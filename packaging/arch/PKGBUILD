# PKGBUILD – Hydra CAD für Arch Linux / AUR
# Nutzung: makepkg -si (lokal) oder auf AUR legen → yay -S hydracad
# pkgver bei neuem Release anpassen (z. B. 3.0.7).

pkgname=hydracad
pkgver=3.0.20
pkgrel=1
pkgdesc="Professional CAD application (C++/Qt6)"
arch=('x86_64')
url="https://github.com/drixber/CAD"
license=('custom')
depends=('qt6-base' 'qt6-tools' 'qt6-translations')
optdepends=('qt6-network: in-app updates without curl')
makedepends=('cmake' 'git')
source=("git+${url}.git#tag=v${pkgver}")
sha256sums=('SKIP')

build() {
  cd CAD
  cmake -B build -DCMAKE_BUILD_TYPE=Release -DCAD_USE_QT=ON
  cmake --build build -j"$(nproc)"
}

package() {
  cd CAD
  # Real binary in lib; wrapper in PATH so app starts from menu and terminal (Wayland → xcb).
  install -Dm755 build/cad_desktop -t "$pkgdir/usr/lib/hydracad"
  install -Dm755 packaging/linux/cad_desktop.wrapper "$pkgdir/usr/bin/cad_desktop"
  ln -s cad_desktop "$pkgdir/usr/bin/hydracad"
  install -Dm644 packaging/linux/HydraCAD.desktop -t "$pkgdir/usr/share/applications"
  install -Dm644 installer/license.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
